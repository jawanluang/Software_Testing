plugins {
    id 'java-library'
}

group 'edu.depaul'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

def junitDefs(String artifact, String version) {
    return "org.junit.jupiter:${artifact}:${version}"
}

dependencies {
    def junitVersion = "5.7.1"
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation "${junitDefs("junit-jupiter-api", junitVersion)}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testImplementation 'org.mockito:mockito-core:3.5.13'
}

task packageDistribution(type: Zip) {
    dependsOn check
    archiveFileName = "${rootProject.name}.zip"
    destinationDirectory = file("$buildDir/dist")

    from("$projectDir") {
        include "src/**/*"
        include "build.gradle"
        include "gradlew"
        include "gradlew.bat"
        include "settings.gradle"
    }

    into("${rootProject.name}")
}

test {
    ignoreFailures(true)
    useJUnitPlatform {
        excludeTags 'integration-test'
    }

    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

check {
    finalizedBy packageDistribution
}

